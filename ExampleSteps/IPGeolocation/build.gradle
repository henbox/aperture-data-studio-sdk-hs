
plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
    maven {
        url 'https://raw.githubusercontent.com/experiandataquality/aperture-data-studio-sdk/github-maven-repository/maven'
    }
}

dependencies {
    compileOnly(
            "com.experian.datastudio:sdkapi:2.6.0",
            "com.experian.datastudio:sdklib:2.6.0"
    )
    implementation(
            'commons-io:commons-io:2.11.0',
            "org.json:json:20220320",
            "org.apache.commons:commons-lang3:3.12.0",
            'org.apache.logging.log4j:log4j-api:2.18.0',
            'org.apache.logging.log4j:log4j-core:2.18.0'
    )
    testImplementation "com.experian.datastudio:sdktestframework:2.6.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

/*
Package the step into a jar together with its dependencies.
Relocate the dependencies to ensure dependencies version doesn't conflict with data studio
*/
shadowJar {
    //minimize() - DO NOT USE minimize() because classes initialized through reflections or service loader pattern will be excluded
    zip64 = true
    relocate 'org.apache.commons.io', 'example.org.apache.commons.io'
    relocate 'org.apache.commons.lang3', 'example.org.apache.commons.lang3'
    relocate 'org.apache.http', 'example.org.apache.http'
    relocate 'org.json', 'example.org.json'
}

tasks.test.dependsOn tasks.shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)